plugins {
    id "java"
    id "jacoco"
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "${springDependencyManagementVersion}"
    id "com.diffplug.spotless" version "${spotlessVersion}"
    id "org.sonarqube" version "${sonarQubeVersion}"
}

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    // Web
    implementation "org.springframework.boot:spring-boot-starter-web"
    // Jpa
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    // Validation
    implementation "org.springframework.boot:spring-boot-starter-validation"
    // Lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    // Mapstruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    // Bcrypt
    implementation "org.springframework.security:spring-security-crypto:${springSecurityCryptoVersion}"
    // Security
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    // JDBC Connector
    runtimeOnly "com.mysql:mysql-connector-j"
    // Test containers
    implementation platform("org.testcontainers:testcontainers-bom:${testContainers}")
    testImplementation "org.testcontainers:mysql"
    testImplementation "org.testcontainers:junit-jupiter"

    // Starter test
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    // Security test
    testImplementation "org.springframework.security:spring-security-test"
    // Jackson converter
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonDatatypeJsr310Version}"
    // H2
    testImplementation "com.h2database:h2:${h2DatabaseVersion}"
}

compileJava {
    options.compilerArgs = [
            "-parameters",
            "-Amapstruct.suppressGeneratorTimestamp=true",
            "-Amapstruct.defaultComponentModel=spring",
            "-Amapstruct.verbose=true"
    ]
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }
}

jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/franktranvantu/springboot3/configuration/**',
                    'com/franktranvantu/springboot3/dto/**',
                    'com/franktranvantu/springboot3/entity/**',
                    'com/franktranvantu/springboot3/enums/**',
                    'com/franktranvantu/springboot3/exeption/**',
                    'com/franktranvantu/springboot3/mapper/**',
                    'com/franktranvantu/springboot3/repository/**',
                    'com/franktranvantu/springboot3/validator/**'

            ])
        }))
    }
}

spotless {
    java {
        removeUnusedImports()
        palantirJavaFormat()
    }
}

sonar {
    properties {
        property("sonar.projectKey", "spring-boot-3")
        property("sonar.login", "sqp_5417d0f35e13b4ef33f3515d2534af6897777b76")
        property("sonar.host.url", "http://localhost:9000")
        property("sonar.exclusions", [
                'com/franktranvantu/springboot3/configuration/**',
                'com/franktranvantu/springboot3/dto/**',
                'com/franktranvantu/springboot3/entity/**',
                'com/franktranvantu/springboot3/enums/**',
                'com/franktranvantu/springboot3/exeption/**',
                'com/franktranvantu/springboot3/mapper/**',
                'com/franktranvantu/springboot3/repository/**',
                'com/franktranvantu/springboot3/validator/**'

        ])
    }
}